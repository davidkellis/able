package able.collections.linked_list

struct List T {
  first: Option Node.T,
  last: Option Node.T
}
// creates fn List(first: Option Node.T, last: Option Node.T) -> List T

struct Node T {
  prev: Option Node.T
  next: Option Node.T
  data: T
}

fn List[T]() -> List T {
  List T {
    first = None
    last = None
  }
  // or equivalently, List T {}
}

fn List(values: T*) -> List T {
  list = List(None, None)
  values.each(list.append)
  list
}

fn append(list: List T, value: T) -> Node T {
  old_last = list.last
  new_last = Node(old_last, None, value)
  list.last = new_last
  if old_last == None {
    list.first = new_last
  }
  new_last
}

fn prepend(list: List T, value: T) -> Node T {
  old_first = list.first
  new_first = Node(None, old_first, value)
  list.first = new_first
  if old_first == None {
    list.last = new_first
  }
  new_first
}

fn find_node(list: List T, value: T) -> Option Node.T => list.find(_.data == value)

fn remove(list: List T, value: T) -> Option Node.T {
  node = list.find_node(value)
  list.remove_node(node) if node
}

fn remove_node(list: List T, node: Node T) -> Unit {
  node match {
    case Node { prev: Some Node.T=prev, next: Node T=next } => {   // interior node
      next.prev = prev
      prev.next = next
    }
    case Node { prev: None, next: Some Node.T=next } => {           // first node
      list.first = next
      next.prev = nil
    }
    case Node { prev: Some Node.T=prev, next: None } => {           // last node
      list.last = prev
      prev.next = nil
    }
  }
}

fn clear(list: List T) -> List T {
  list.first = None
  list.last = None
  list
}


////////////////////////////////// Interface implementations //////////////////////////////////

ListValues = impl Iterable T for List T {
  fn each(list: List T, f: T -> Unit) -> Unit {
    node = list.first
    while node != nil {
      f(node.data)
      node = node.next
    }
  }
}

ListNodes = impl Iterable Node.T for List T {
  fn each(list: List T, f: T -> Unit) -> Unit {
    node = list.first
    while node != nil {
      f(node)
      node = node.next
    }
  }
}